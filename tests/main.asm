    MOV64 SP, STK_TOP
    JMP MAIN
KEY_PRESSED:
    ; Key = 0xA000
    MOV32 G3, [0xA000]
    CMP32 G3, 340
    JE KEY_SHIFT
    MOV8 G4, [0xA004]
    CMP8 G4, 1
    JE KEY_DOWN
    JMP KEY_EXIT
KEY_DOWN:
    CMP32 G3, 257 ; ENTER
    JE KEY_ENTER
    CMP32 G3, 259
    JE KEY_BACKSPACE
    MOV32 G1, [CURSOR_X]
    MOV32 G2, [CURSOR_Y]
    MOV8 G3, [0xA000]
    CMP8 [SHIFT], 1
    JE KEY_DOWN_S
    MOV8 G3, [KB_MAP_NS+G3]
    JMP KEY_DOWN_C
KEY_DOWN_S:
    MOV8 G3, [KB_MAP_S+G3]
KEY_DOWN_C:
    CALL PUT_CHAR
    MOV64 G4, [BUFFER_IDX]
    MOV8 [BUFFER+G4], G3
    ADD64 [BUFFER_IDX], 1
    ADD32 [CURSOR_X], 1
    CALL UPDATE_CURSOR
    JMP KEY_EXIT
KEY_SHIFT:
    MOV8 G4, [0xA004] ; STATUS
    CMP8 G4, 1 ; DOWN
    JE KEY_APPLY_SHIFT
    MOV8 [SHIFT], 0
    JMP KEY_EXIT
KEY_APPLY_SHIFT:
    MOV8 [SHIFT], 1
    JMP KEY_EXIT
KEY_BACKSPACE:
    CMP64 [BUFFER_IDX], 0
    JE KEY_EXIT
    SUB32 [CURSOR_X], 1
    CALL UPDATE_CURSOR
    SUB64 [BUFFER_IDX], 1
    MOV64 G4, [BUFFER_IDX]
    MOV8 [BUFFER+G4], 0
    MOV32 G1, [CURSOR_X]
    MOV32 G2, [CURSOR_Y]
    MOV8 G3, 0
    CALL PUT_CHAR
    JMP KEY_EXIT
KEY_ENTER:
    MOV32 [CURSOR_X], 0
    ADD32 [CURSOR_Y], 1
    CALL UPDATE_CURSOR
    MOV64 G4, [BUFFER_IDX]
    MOV8 [BUFFER+G4], 0
    MOV64 [BUFFER_IDX], 0
    CALL SHELL_ENTER
KEY_EXIT:
    RET
TABLE:
    D64 KEY_PRESSED
    D8 1 ; PRESENT FLAG
IVT:
    D64 TABLE
    D8 1 ; COUNT
SHELL_ENTER:
    MOV64 G0, BUFFER
    MOV64 G1, HELP_CMD
    CALL STRCMP
    CMP8 G2, 0 ; SUCCESS
    JE SHELL_HELP
    MOV64 G0, BUFFER
    MOV64 G1, FETCH_CMD
    CALL STRCMP
    CMP8 G2, 0 ; SUCCESS
    JE SHELL_FETCH
    JMP SHELL_ENTER_EXIT
SHELL_HELP:
    MOV64 G0, HELP_MSG
    CALL PRINT
    JMP SHELL_ENTER_EXIT
SHELL_FETCH:
    MOV64 G0, FETCH_MSG
    CALL PRINT
    JMP SHELL_ENTER_EXIT
SHELL_ENTER_EXIT:
    MOV64 G0, INPUT
    CALL PRINT
    RET
MAIN:
    MOV64 IVTBL, IVT
    SEI
    MOV64 G0, SHELL
    CALL PRINT
    MOV64 G0, INPUT
    CALL PRINT
LOOP:
    JMP LOOP

SHELL:
    D8 "Astro64 SHELL V 0.1"
    D8 10 D8 0
INPUT:
    D8 "SHELL> "
    D8 0

HELP_MSG:
    D8 "Astro64 shell V 0.1"
    D8 10
    D8 "Astro64 Designed by Astrido"
    D8 10
    D8 0

FETCH_MSG:
    D8 "Astro64 simple firmware, running on Astro64 CPU."
    D8 10
    D8 "Memory: (TODO)"
    D8 10
    D8 "Disk Space: (TODO)"
    D8 10
    D8 0

HELP_CMD:
    D8 "help"
    D8 0

FETCH_CMD:
    D8 "fetch"
    D8 0

BUFFER: RES8 64
BUFFER_IDX: D64 0

SHIFT: D8 0

%INCLUDE "tests/string.asm"
%INCLUDE "tests/terminal.asm"
%INCLUDE "tests/kb_map.asm"

STK_BOTTOM: RES8 1024
STK_TOP: D8 0
EXIT:
    JMP EXIT
